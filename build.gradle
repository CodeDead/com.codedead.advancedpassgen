plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.19.0'
}

group 'com.codedead'
version '2.0-SNAPSHOT'

sourceCompatibility = 14
targetCompatibility = 14

java {
    modularity.inferModulePath = true
}

application {
    mainModule = 'AdvancedPassGen'
    mainClass = 'com.codedead.advancedpassgen.JavaFXApplication'
}

clean.doFirst {
    delete 'default.properties'
    delete 'license.pdf'
    delete 'help.pdf'
    delete './logs'
}

javafx {
    version = '14.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = "Advanced PassGen"
        noConsole = true
    }
    forceMerge('log4j-api')
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}
